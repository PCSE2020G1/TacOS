#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#include <syslib.hmm>                    // TaC版は一部のシステムコールを使う
#include "buffer.hmm"
#include "screen.hmm"

char[] kbdBuf = array(BUFLEN);            // 入力用
boolean isEsc= false;

// 入力文字の処理
void keyboard(char key){
  if(key == '\x1b'){
    isEsc = true;
    return;
  } else if(isEsc){
    if(key == '['){
      return;
    }
    if (key == 'A'){  // up
      moveUp();
      if (lines >= COLUMNS){
        displayAll();
      }
    }else if (key == 'B'){  // down
      moveDown();
      if (lines >= COLUMNS){
        displayAll();
      }
    } else if (key == 'C'){ // right
      moveRight();
    } else if (key == 'D'){  // left
      moveLeft();
    }
    isEsc = false;
    moveCursor();
    return;
  } else if (key == '\x7f'){    // del
    // 行の長さが０または右端
    if(strLen(textBuffer[current]) == 0 || c.x == 0){
      removeLine(c.x, current);
      if(current > 0){
        moveUp();
        moveToEndOfRow();
      }
      displayAll();
    } else {
      removeChar(c.x, current);
      displayLine();
      moveLeft();
    }
    moveCursor();
    return;
  } else if (key == '\r' || key == '\n'){
    printf("\r");
    insertLine(c.x, current);
    insertChar('\n', c.x, current);
    moveDown();
    if (lines >= COLUMNS || c.y+1 != lines || c.x != strLen(textBuffer[c.y])){
      displayAll();
    }
    c.x = 0;

    moveCursor();
    return;
  }
  insertChar(key, c.x, current);
  if(c.x+1 == strLen(textBuffer[current]) || textBuffer[current][c.x+1] == '\n'){
    printf("%c", key);
  } else {
    displayLine();
  }
  moveRight();
  moveCursor();

  return;
}

// ファイルの内容を処理
void loadFile(char[] fileName) {
  FILE fp;
  char ch;
  fp = fopen(fileName, "r"); // ファイルを開く
  if (fp == null) {
    perror(fileName);
    printf("\x1b[2J\x1b[H");
  } else {
    while(!feof(fp)) {
      ch = fgetc(fp);
      keyboard(ch);
    }
    fclose(fp);
  }
}

// ファイルに書き込む
void saveFile(char[] fileName) {
  remove(fileName);

  FILE fp;
  fp = fopen(fileName, "w");
  if (fp == null) {
    perror(fileName);
    return;
  }
  for(int i = 0; i <= lines; i = i + 1){
    fprintf(fp, "%s", textBuffer[i]);
  }
  fclose(fp);
}

public int main(int argc, char[][] argv){
  if(argc!=2){
    fprintf(stderr, "Usage, %s <filename>\n", argv[0]);
    return 1;
  }
  b_init();
  s_init();

  char[] fileName = argv[1];

  char ch;
  int orgMode = ttyCtl(TTYCTL_GETMODE, 0);     // 普通のモード
  int rawMode = orgMode & ~TTYCTL_MODE_ECHO & ~TTYCTL_MODE_COOKED;  // １文字入力

  stdout.buf = null;  // stdoutのバッファリングを禁止
  clear();

  // ファイル読み込み
  loadFile(fileName);

  ttyCtl(TTYCTL_SETMODE, rawMode);
  while(true){
    // 文字入力
    ttyRead(kbdBuf, 1);

    // qを押したら終わり
    ch = kbdBuf[0];
    if(isEsc && ch == 'q'){
      break;
    }
    keyboard(ch);
  }
  ttyCtl(TTYCTL_SETMODE, orgMode);

  // save
  saveFile(fileName);

  printf("\r\n");
  for(int i = 0; i <= lines; i = i + 1){
    printf("%d: %s", i, textBuffer[i]);
  }
  printf("\r\n");
  return 0;
}
