#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#include <syslib.hmm>                    // TaC版は一部のシステムコールを使う
#include "buffer.hmm"
#include "display.hmm"

char[] kbdBuf = array(BUFLEN + 1);            // 入力用
boolean isEsc= false;

// 入力文字の処理
void keyboard(char key){
    if(key == '\x1b'){
      isEsc = true;
      return;
    } else if(isEsc){
      if (key == 'A'){  // up
        moveUp();
        displayAll();
        printf("\x1b[1A");
        isEsc = false;
      }else if (key == 'B'){  // down
        moveDown();
        displayAll();
        printf("\x1b[1B");
        isEsc = false;
      } else if (key == 'C'){ // right
        moveRight();
        printf("\x1b[1C");
        isEsc = false;
      } else if (key == 'D'){  // left
        moveLeft();
        printf("\x1b[1D");
        isEsc = false;
      }
      return;
    } else if (key == '\x7f'){    // del
      removeChar();
      moveLeft();
      displayAll();
      moveCursor();
      return;
    } else if (key == '\r' || key == '\n'){
      printf("\r");
      insertChar('\n');
      addNewLine();
      if (lines >= COLUMNS){
        displayAll();
      }
      moveCursor();
      return;
    }
    insertChar(key);
    moveRight();
    printf("%c", key);
    return;
}

// ファイルの内容を処理
void loadFile(char[] fileName) {
  FILE fp;
  char ch;
  fp = fopen(fileName, "r"); // ファイルを開く
  if (fp == null) {
    perror(fileName);
    printf("\x1b[2J\x1b[H");
  } else {
    while(!feof(fp)) {
      ch = fgetc(fp);
      keyboard(ch);
    }
    fclose(fp);
  }
}

// ファイルに書き込む
void saveFile(char[] fileName) {
  remove(fileName);

  FILE fp;
  fp = fopen(fileName, "w");
  if (fp == null) {
    perror(fileName);
    return;
  }
  for(int i = 0; i <= lines; i = i + 1){
    fprintf(fp, "%s", textBuffer[i]);
  }
  fclose(fp);
}

public int main(int argc, char[][] argv){
  if(argc!=2){
    fprintf(stderr, "Usage, %s <filename>\n", argv[0]);
    return 1;
  }
  init();
  char[] fileName = argv[1];

  char ch;
  int orgMode = ttyCtl(TTYCTL_GETMODE, 0);     // 普通のモード
  int rawMode = orgMode & ~TTYCTL_MODE_ECHO & ~TTYCTL_MODE_COOKED;  // １文字入力

  stdout.buf = null;  // stdoutのバッファリングを禁止
  clear();

  // ファイル読み込み
  loadFile(fileName);

  ttyCtl(TTYCTL_SETMODE, rawMode);
  while(true){
    // 文字入力
    ttyRead(kbdBuf, 1);

    // qを押したら終わり
    ch = kbdBuf[0];
    if(isEsc && ch == 'q'){
      break;
    }
    keyboard(ch);
  }
  ttyCtl(TTYCTL_SETMODE, orgMode);

  // save
  saveFile(fileName);

  for(int i = 0; i <= lines; i = i + 1){
    printf("%d: %s", i, textBuffer[i]);
  }

  printf("\r\n");
  return 0;
}
