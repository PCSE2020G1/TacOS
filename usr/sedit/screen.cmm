#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#include "buffer.hmm"
#include "screen.hmm"

public cursor c = {0, 0};

public void s_init(){
  top = 0;
  current = 0;
}

// 画面クリア
public void clear(){
  printf("\x1b[2J\x1b[H");
}

// カーソル移動
public void moveCursor(){
  printf("\x1b[%d;%dH", (c.y+1), (c.x+1));
}

// デバッグ用
public void printBufferInfo(){
  printf("\x1b[%d;%dH", COLUMNS, 1);
  printf("\r\x1b[2K");
  printf("(%d, %d), current:%d , top:%d, lines:%d, length:%d", c.x, c.y, current, top, lines, strLen(textBuffer[current]));
  moveCursor();
}

// 全画面表示
public void displayAll(){
  // 画面クリア
  printf("\x1b[2J\x1b[H");
  int end = lines + 1;
  if(end >  COLUMNS){
    end = COLUMNS;
  }
  // 表示
  for(int i = top; i < top + end; i = i + 1){
    printf("\r");
    printf("%s", textBuffer[i]);
    if (strRchr(textBuffer[i], '\n') == -1) {
      if(i != top + end - 1){
        printf("\n");
      }
    }
  }
}

// 現在の行を更新
public void displayLine(){
  printf("\r\x1b[2K");
  printf("%s", textBuffer[current]);
}

// カーソルを右へ
public void moveRight(){
  c.x = c.x + 1;
  // 最大数を超えない

  if(c.x >= ROWS){
    c.x = ROWS - 1;
  } else if(c.x >= strLen(textBuffer[current])){
    c.x = strLen(textBuffer[current]);
  }
}

//　カーソルを左へ
public void moveLeft(){
  c.x = c.x - 1;
  // 0より小さくならない
  if(c.x < 0){
    c.x = 0;
  }
}

// カーソルを下へ
public void moveDown(){
  if(current+1 > lines){
    return;
  }

  c.x = 0;  // xを左端に移動
  c.y = c.y + 1;
  current = current + 1;

  if(c.y == COLUMNS){
    c.y = COLUMNS - 1;
    // 下に表示可能な部分があれば一番上の行を更新
    if(top + (COLUMNS-1) < lines){
      top = top + 1;
    }
  }
}

// カーソルを上へ
public void moveUp(){
  if(current == 0){
    return;
  }
  c.x = 0;
  current = current - 1;
  c.y = c.y - 1;

  if(c.y < 0){
    c.y = 0;
    // 上に表示可能な部分があれば一番上の行を更新
    if(top > 0){
      top = top - 1;
    }
  }
}
