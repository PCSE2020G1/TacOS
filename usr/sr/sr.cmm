/*
 * TacOS Source Code
 *    Tokuyama kousen Advanced educational Computer.
 *
 * Copyright (C) 2019 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 *
 */

/*
 * sr/sr.cmm : ファイルをシリアルから受信する
 *
 * 2019.07.11 : 藤村くんのプログラムを取り込む
 *
 * $Id$
 *
 */

//#define DEBUG

#if defined(DEBUG)
# define debug(...) fprintf(stderr, __VA_ARGS__)
#else
# define debug(...) /* __VA_ARGS__ */
#endif

#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#include <ctype.hmm>
#define ACK "!Ack\n"
#define N 81
#define BSIZE_MAX ((N-2)*3/4)
char[] lbuf=array(N);
char[] bbuf=array(BSIZE_MAX);

void bwrite(FILE fp, char[] bbuf, int bsize) {
  for (int i=0; i<bsize; i=i+1) {
    fputc(bbuf[i], fp);
  }
}

int a2b(char a) {
  if ('=' == a) return -1;
  if (isUpper(a)) return ord(a) - ord('A');
  if (isLower(a)) return ord(a) - ord('a') + 26;
  if (isDigit(a)) return ord(a) - ord('0') + 52;
  if ('+' == a) return 62;
  if ('/' == a) return 63;
  fprintf(stderr, "Error: invalid character\n");
  exit(1);
  return -2; /* エラー避け */
}

int b64d(char[] out, int outB, char[] in, int inB) {
  int w = a2b(in[inB]);
  int x = a2b(in[inB + 1]);
  if (w < 0 || x < 0) {
    fprintf(stderr, "Error: === is not allowed\n");
    exit(1);
  }
  out[outB] = chr((w << 2 | x >> 4) & 0xff);
  int y = a2b(in[inB + 2]);
  if (y < 0) return 1;
  out[outB + 1] = chr((x << 4 | y >> 2) & 0xff);
  int z = a2b(in[inB + 3]);
  if (z < 0) return 2;
  out[outB + 2] = chr((y << 6 | z) & 0xff);
  return 3;
}

public int main(int argc, char[][] argv) {
  if (argc>1) {
    debug("Usage: %s\n", argv[0]);
    return 1;
  }

  fgets(lbuf, N, stdin);
  int pos=strRchr(lbuf, '\n');
  if (pos==-1) {
    debug("format error");
    exit(1);
  }

  lbuf[pos] = '\0';

  debug("fopen:%s\n",lbuf);
  FILE fp = fopen(lbuf, "w");
  if (fp==null) {
    perror(lbuf);
    exit(1);
  }

  fgets(lbuf, N, stdin);
  int fsize = atoi(lbuf);
  debug("filelength:%s:%d\n",lbuf,fsize);

  fgets(lbuf, N, stdin);
  int bsize = atoi(lbuf);
  debug("blocksize:%s:%d\n",lbuf,bsize);
  if (bsize > BSIZE_MAX) {
    fprintf(stderr, "Error: block size is up to %d\n", BSIZE_MAX);
    exit(1);
  }

  int cbsize = 0;
  int cfsize = 0;
  while (cfsize<fsize) {
    fgets(lbuf, N, stdin);
    /// [入力のパース]
    int llen = strLen(lbuf) - 1;
    if (llen % 4 != 0) { // 入力サイズが4の倍数になっていない→エラー終了
      fprintf(stderr, "Error: input length must be 4n\n");
      exit(1);
    }
    for (int k = 0; k < llen; k = k + 4) {
      int dlen = b64d(bbuf, cbsize, lbuf, k);
      cbsize = cbsize + dlen;
      cfsize = cfsize + dlen;
    }

    debug("file: %d / %d, buf: %d / %d\n", cfsize, fsize, cbsize, bsize);

    if (cbsize > bsize || cfsize > fsize) {
      fprintf(stderr, "!overflow!\n");
      exit(1);
    }
    /// [/入力のパース]

    bwrite(fp, bbuf, cbsize);
    cbsize = 0;
    printf(ACK);

  }
  fclose(fp);

  return 0;
}
